plugins {
    id 'application'
    id 'com.github.johnrengelman.shadow' version '8.1.1'
}

repositories {
    mavenCentral()
}

ext {
    javaVersion = 22

    lombokVersion = '1.18.34'
    mapStructVersion = '1.6.0.Beta1'
}

dependencies {
    implementation platform('com.fasterxml.jackson:jackson-bom:2.17.2')
    implementation 'com.fasterxml.jackson.core:jackson-databind'

    implementation platform('org.jboss.weld:weld-core-bom:6.0.0.Beta4')
    implementation 'org.jboss.weld.se:weld-se-core'

    implementation platform('com.google.guava:guava-bom:33.3.1-jre')
    implementation 'com.google.guava:guava'

    implementation 'com.github.wendykierp:JTransforms:3.1'
    implementation 'org.jfree:jfreechart:1.0.19'

    compileOnly "org.projectlombok:lombok:${lombokVersion}"
    annotationProcessor "org.projectlombok:lombok:${lombokVersion}"
    compileOnly "org.mapstruct:mapstruct:${mapStructVersion}"
    annotationProcessor 'org.projectlombok:lombok-mapstruct-binding:0.2.0'
    annotationProcessor "org.mapstruct:mapstruct-processor:${mapStructVersion}"

    testImplementation platform('org.junit:junit-bom:5.11.3')
    testImplementation 'org.junit.jupiter:junit-jupiter'
    testRuntimeOnly "org.junit.platform:junit-platform-launcher"

    testImplementation platform('org.assertj:assertj-bom:3.26.3')
    testImplementation 'org.assertj:assertj-core'

    testCompileOnly "org.projectlombok:lombok:${lombokVersion}"
    testAnnotationProcessor "org.projectlombok:lombok:${lombokVersion}"
}

application {
    applicationDefaultJvmArgs = ['-XX:+UseZGC', '-XX:+ZGenerational', '-Xmx4G', '-XX:MaxDirectMemorySize=1G']
    mainClass = 'org.jboss.weld.environment.se.StartMain'
}

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(javaVersion)
    }
}

sourceSets {
    main {
        java {
            // put resources in classes output
            output.resourcesDir = 'build/classes/java/main'
        }
    }
}

test {
    useJUnitPlatform()
}
